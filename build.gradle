buildscript {
    repositories {
        jcenter() // Shadow plugin
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    }
}

defaultTasks 'clean', 'build'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow' // Shadow jar

    group 'com.alessiodp.securityvillagers'
    version '2.10.1'

    // Info
    ext.pluginName = "SecurityVillagers"
    ext.pluginDescription = "Customize your villagers protection"
    ext.pluginAuthor = "AlessioDP"
    ext.pluginWebsite = "https://alessiodp.com/"

    // Development
    ext.adpCore = '1.2.7'
    ext.lombok = '1.18.10'
    ext.powermock = '2.0.2'

    // Libraries
    ext.protocolLib = '4.4.0'

    // Misc
    sourceCompatibility = 1.8

    repositories {
        mavenLocal()

        // Maven central
        mavenCentral()
        // CodeMC repository - ADP Core
        maven { url = 'https://repo.codemc.org/repository/maven-public/' }
        // CodeMC repository - NMS repo
        maven { url = 'https://repo.codemc.org/repository/nms/' }
        // Spigot repository for Spigot & Bukkit
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        // OSS Sonatype repository for Bungeecord
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        // JitPack repo
        maven { url = 'https://jitpack.io' }
        // Sponge repository for configurate
        maven { url = 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok

        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: project.ext.powermock
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: project.ext.powermock
    }
}

dependencies {
    compile project(':common')
    compile project(':bukkit')
    compile project(':bukkit:v1_8_R3')
    compile project(':bukkit:v1_9_R1')
    compile project(':bukkit:v1_9_R2')
    compile project(':bukkit:v1_10_R1')
    compile project(':bukkit:v1_11_R1')
    compile project(':bukkit:v1_12_R1')
    compile project(':bukkit:v1_13_R1')
    compile project(':bukkit:v1_13_R2')
    compile project(':bukkit:v1_14_R1')
}

// Disable root project jar
jar.enabled = false

shadowJar {
    archiveBaseName  = project.ext.pluginName
    archiveClassifier = null
    dependencies {
        include(dependency('com.alessiodp.securityvillagers:.*'))
        include(dependency('com.alessiodp.core:.*'))
    }

    relocate 'com.alessiodp.core', 'com.alessiodp.securityvillagers.core'
}

artifacts {
    archives shadowJar
}
